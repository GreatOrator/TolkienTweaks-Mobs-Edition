import groovy.json.JsonOutput

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

group = "com.greatorator.tolkienmobs"
archivesBaseName = "TolkienMobs"

sourceSets.main.resources.srcDirs += "src/main/generated"

mod_version = "${mod_version}." + (System.getenv("BUILD_NUMBER") ?: "9999")
version = "${mc_version}-${mod_version}"
println "Starting build of ${archivesBaseName}, Version: ${mod_version}"
println "Using Forge: ${neo_version}, for Minecraft: ${mc_version}"

minecraft {
    mappings channel: 'official', version: mc_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        client {
            workingDirectory file('run')
            mods { tolkienmobs { source sourceSets.main } }
        }
        server {
            workingDirectory file('run')
            mods { tolkienmobs { source sourceSets.main } }
        }
        data {
            workingDirectory file('run')
            args '--mod', 'tolkienmobs', '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods { tolkienmobs { source sourceSets.main } }
        }
    }
}

mixin {
    add(sourceSets.main, "mixins.tolkienmobs.refmap.json")
}

configurations {
    annotationProcessor.extendsFrom minecraft
}

repositories {
    mavenLocal()
    maven { url = "https://maven.covers1624.net/" }
    maven { url = "https://dvs1.progwml6.com/files/maven" }
    maven { url = "https://maven.cil.li/" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url = "https://squiddev.cc/maven/" }
    maven { url = "https://maven.blamejared.com/" }
//    maven { url = "https://modmaven.dev/" }
}

dependencies {
    minecraft "net.neoforged:forge:${mc_version}-${neo_version}"

    implementation fg.deobf("com.brandon3055.brandonscore:BrandonsCore:${mc_version}-${bcore_version}:universal")
    implementation fg.deobf("io.codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

    compileOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api"))
    runtimeOnly(fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}"))
}

test {
    useJUnitPlatform()
}

processResources {
    inputs.property 'mod_version', mod_version
    inputs.property 'mc_version', mc_version
    inputs.property 'ccl_version', ccl_version
    inputs.property 'bcore_version', bcore_version

    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': mod_version],
                'mc_version': mc_version,
                'neo_version': neo_version,
                'bcore_version': bcore_version,
                'cct_version': cct_version,
                'lang_version': neo_version.split('\\.')[0],
                'ccl_version': "[${ccl_version.replace(".+", "")},${ccl_version_max})"
    }
}

def publishingMetadata = project.layout.buildDirectory.file("libs/$archivesBaseName-$version-publishing.json")
def publishingMetadataTask = tasks.register("publishingMetadata") {
    outputs.file(publishingMetadata)
    doFirst {
        publishingMetadata.get().asFile.text = JsonOutput.prettyPrint(JsonOutput.toJson([
                [
                        'displayName' : "Tolkien Tweaks - Mobs Edition ${version}",
                        'mcVersion'   : "${mc_version}",
                        'version'     : "${mod_version}",
                        'classifier'  : 'universal',
                        'modLoader'   : ['neoforge', 'forge'],
                        'dependencies': [
                                [
                                        'modId'       : 'codechickenlib',
                                        'type'        : 'required',
                                        'modrinthId'  : '2gq0ALnz',
                                        'curseforgeId': 'codechicken-lib-1-8'
                                ],
                                [
                                        'modId'       : 'brandonscore',
                                        'type'        : 'required',
                                        'modrinthId'  : 'iFDWVIFV',
                                        'curseforgeId': 'brandons-core'
                                ]
                        ]
                ]
        ]))
    }
}

jar {
    finalizedBy 'reobfJar'
    finalizedBy 'signJar'
    archiveClassifier = 'universal'
    manifest {
        attributes 'Specification-Title': archivesBaseName
        attributes 'Specification-Vendor': 'greatorator'
        attributes 'Specification-Version': "1"
        attributes 'Implementation-Title': archivesBaseName
        attributes 'Implementation-Vendor': 'greatorator'
        attributes 'Implementation-Version': version
        attributes 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attributes 'MixinConfigs': 'mixins.tolkienmobs.json'
    }
    from file("LICENSE")
}

tasks.register("signJar") {
    dependsOn("jar")
    dependsOn("reobfJar")

    doFirst {
        def args = [:]
        args['jar'] = jar.archiveFile.get().asFile
        if (System.getenv("KEY_STORE")) {
            args['keyStore'] = System.getenv("KEY_STORE")
            file(System.getenv("KEY_STORE_PROPS")).withReader {
                def props = new Properties()
                props.load(it)
                args.putAll(props)
            }
        } else if (project.hasProperty('keyStore')) {
            args['keyStore'] = project.getProperty('keyStore')
            args['alias'] = project.getProperty('keyStoreAlias')
            args['storePass'] = project.getProperty('keyStorePass')
            args['keyPass'] = project.getProperty('keyStoreKeyPass')
        } else {
            println "No signing properties."
            state.setOutcome(org.gradle.api.internal.tasks.TaskExecutionOutcome.UP_TO_DATE)
            didWork = false
            return
        }
        project.ant.invokeMethod('signjar', args)
    }
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'greatorator'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        tolkienmobs(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            artifact jar
            artifact sourcesJar
            artifact(publishingMetadata) {
                builtBy publishingMetadataTask
                classifier 'publishing'
            }
            pom {
                name = archivesBaseName
                description = archivesBaseName
                url = "https://github.com/greatorator/${archivesBaseName}"
                scm {
                    url = "https://github.com/greatorator/${archivesBaseName}"
                    connection = "scm:git:git://github.com/greatorator/${archivesBaseName}.git"
                    connection = "scm:git:git@github.com:greatorator/${archivesBaseName}.git"
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/greatorator/${archivesBaseName}/issues"
                }
                licenses {
                    license {
                        name = "CoFH \"Don't Be a Jerk\" License"
                        url = "https://github.com/greatorator/Draconic-Evolution/blob/master/LICENSE"
                        distribution = 'Repo'
                    }
                }
                developers {
                    developer {
                        id = 'greatorator'
                        name = 'greatorator'
                    }
                }
            }
        }
    }
}

signing {
    if (System.getenv('GPG_PRIVATE_KEY')) {
        useInMemoryPgpKeys(
                new File(System.getenv('GPG_PRIVATE_KEY')).text,
                System.getenv('GPG_PRIVATE_KEY_PASS')
        )
    } else if (project.hasProperty('gpgPrivateKey')) {
        useInMemoryPgpKeys(
                new File(project.getProperty('gpgPrivateKey')).text,
                project.getProperty('gpgPrivateKeyPass')
        )
    } else {
        return
    }
    sign publishing.publications.tolkienmobs
}